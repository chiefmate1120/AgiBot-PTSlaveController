<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4024.11">
  <POU Name="FB_CartCtrlMotionCar" Id="{d05e52e3-1460-4326-9432-80246d34dd7a}" SpecialFunc="None">
    <Declaration><![CDATA[FUNCTION_BLOCK FB_CartCtrlMotionCar EXTENDS FB_CartCtrlBase
VAR_INPUT
END_VAR
VAR_OUTPUT
END_VAR
VAR
	
	// time threshold to drive wheel after it's enabled
	m_cartWheelDriveWaitTime :LREAL := 0.2;
	
	//wheel motion  time 
	m_wheelMotionTime:  LREAL;
	
	// new strategy
	m_mass :LREAL :=1;
	m_friction : LREAL :=2;
	m_cmdLinearAcc :LREAL:=0;
	m_maxLinearAcc:LREAL :=2*pi;
	m_cmdLinearVel :LREAL :=0;
	m_maxLinearVel :LREAL := pi;
	m_staticVelThres :LREAL:=0;
	m_pushForce :LREAL :=0;
	m_startMove :BOOL:=FALSE;
	m_linearStatic :BOOL := TRUE;
	m_netForce :LREAL:=0;
	
	
	m_inertia :LREAL :=1;
	m_wFriction :LREAL :=1;
	m_cmdAngAcc:LREAL:=0;
	m_maxAngAcc:LREAL:=2.0;
	m_cmdAngVel :LREAL :=0;
	m_staticWVelThres :LREAL:=0;
	m_maxAngVel :LREAL :=1.0;
	m_turnTorque :LREAL :=0;	
	m_startTurn :BOOL :=FALSE;
	m_angStatic :BOOL :=TRUE;
	m_netTorque:LREAL;

END_VAR]]></Declaration>
    <Implementation>
      <ST><![CDATA[

]]></ST>
    </Implementation>
    <Method Name="calcCmdJntVel" Id="{764cdcc2-44fc-4de0-8a50-0d1f87c1ea9c}">
      <Declaration><![CDATA[// override this function in each exact controller
METHOD PROTECTED calcCmdJntVel : BOOL
VAR_IN_OUT CONSTANT
	// slave cart joint data
	i_slaveCart :FB_SlaveCart;
END_VAR

VAR
	i:INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[m_pushForce :=i_slaveCart.cartDriveForce[2];
m_turnTorque := i_slaveCart.cartDriveForce[1];

m_wheelMotionTime := LIMIT(0, m_wheelMotionTime + g_cartCtrlCycleTime, 1);
IF m_wheelMotionTime< m_cartWheelDriveWaitTime THEN
	RETURN;
END_IF

//linear motion
m_startMove := ABS(m_pushForce)> m_friction;
m_linearStatic := ABS(m_cmdLinearVel)<m_staticVelThres;
IF m_linearStatic THEN
	IF m_startMove THEN
		m_netForce := m_pushForce - sign(m_pushForce)*m_friction;
	ELSE
		m_netForce :=0;
		m_cmdLinearVel:=0;
	END_IF
ELSE
	m_netForce := m_pushForce - sign(m_cmdLinearVel)*m_friction;
END_IF

m_cmdLinearAcc:=LIMIT(-m_maxLinearAcc, m_netForce/m_mass, m_maxLinearAcc);
m_cmdLinearVel:= m_cmdLinearVel + m_cmdLinearAcc * g_cartCtrlCycleTime;
m_cmdLinearVel := LIMIT(-m_maxLinearVel, m_cmdLinearVel, m_maxLinearVel);

//angular motion
m_startTurn := ABS(m_turnTorque)> m_wFriction;
m_angStatic := ABS(m_cmdAngVel)<m_staticWVelThres;
IF m_angStatic THEN
	IF m_startTurn THEN
		m_netTorque := m_turnTorque - sign(m_turnTorque)*m_wFriction;
	ELSE
		m_netTorque :=0;
		m_cmdAngVel:=0;
	END_IF
ELSE
	m_netTorque := m_turnTorque - sign(m_cmdAngVel)*m_wFriction;
END_IF

m_cmdAngAcc:=limit(-m_maxAngAcc,m_netTorque/m_inertia,m_maxAngAcc);
m_cmdAngVel:= m_cmdAngVel + m_cmdAngAcc * g_cartCtrlCycleTime;
m_cmdAngVel := LIMIT(-m_maxAngVel, m_cmdAngVel, m_maxAngVel);

m_cmdJntVel[3]:= m_cmdLinearVel - m_cmdAngVel;
m_cmdJntVel[4]:= m_cmdLinearVel + m_cmdAngVel;
]]></ST>
      </Implementation>
    </Method>
    <Method Name="forceDataAnalysis" Id="{40fc9c46-0fbb-4c32-b63e-2b8d816a336a}">
      <Declaration><![CDATA[METHOD forceDataAnalysis : LREAL
VAR_INPUT
	number : DINT;
END_VAR


VAR
	exponrnt : DINT;
	sign : UDINT;
	mantissa :UDINT;
	value :REAL :=0.5;
	mantissa2 :REAL := 0.0;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[sign :=SHR(number,31);
exponrnt := (( SHR(number,23) AND 16#FF )) - (16#7F);
mantissa := SHL(number,9);
WHILE  mantissa<>0 DO
	IF (mantissa AND 16#80000000) = 16#80000000 THEN
		mantissa2 := mantissa2 + value;
	END_IF
	mantissa := SHL(mantissa,1);
	value := value *0.5;
END_WHILE
value := (1+mantissa2)*( EXPT(2,exponrnt));

IF sign<>0 THEN
	value := -value;
END_IF
forceDataAnalysis := value;]]></ST>
      </Implementation>
    </Method>
    <Method Name="init" Id="{940ec1c0-b19a-4580-a204-bbc4e9c523a4}">
      <Declaration><![CDATA[// override this function in each exact controller
// NOTICE: set joint control mode here, and do some initialization if needed
METHOD PUBLIC init : BOOL
VAR_IN_OUT CONSTANT
	i_slaveCart	:FB_SlaveCart;
END_VAR
VAR_IN_OUT 
	r_cartCtrlCmd :ST_SlaveArmCtrlCmds;
END_VAR
VAR
	i:INT;
END_VAR]]></Declaration>
      <Implementation>
        <ST><![CDATA[//m_holdLastCmds := checkToHoldLastCmds(i_slaveCart);
//m_holdLastCmds := checkToHoldLastCmds(i_slaveCart);
SUPER^.init(i_slaveCart,r_cartCtrlCmd);

m_jntEnableFlag[3] :=1;
m_jntEnableFlag[4] :=1;

m_jntOPMode[3]:= DriverOPMode_csvMZ;
m_jntOPMode[4]:= DriverOPMode_csvMZ;

m_wheelMotionTime:=0;
m_cmdLinearAcc:=0;
m_cmdLinearVel:=0;
m_cmdAngAcc:=0;
m_cmdAngVel:=0;
m_staticVelThres := 2*g_cartCtrlCycleTime*m_friction/m_mass;
m_staticWVelThres := 2*g_cartCtrlCycleTime*m_wFriction/m_inertia;
m_linearStatic:=TRUE;
m_angStatic:=TRUE;

// update commands to arm
copyCmds(r_cartCtrlCmd);]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_CartCtrlMotionCar">
      <LineId Id="52" Count="1" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_CartCtrlMotionCar.calcCmdJntVel">
      <LineId Id="139" Count="1" />
      <LineId Id="151" Count="1" />
      <LineId Id="180" Count="2" />
      <LineId Id="178" Count="0" />
      <LineId Id="177" Count="0" />
      <LineId Id="157" Count="0" />
      <LineId Id="190" Count="0" />
      <LineId Id="202" Count="0" />
      <LineId Id="205" Count="0" />
      <LineId Id="207" Count="0" />
      <LineId Id="209" Count="2" />
      <LineId Id="208" Count="0" />
      <LineId Id="203" Count="0" />
      <LineId Id="206" Count="0" />
      <LineId Id="204" Count="0" />
      <LineId Id="162" Count="1" />
      <LineId Id="148" Count="0" />
      <LineId Id="188" Count="0" />
      <LineId Id="150" Count="0" />
      <LineId Id="164" Count="0" />
      <LineId Id="222" Count="10" />
      <LineId Id="175" Count="0" />
      <LineId Id="221" Count="0" />
      <LineId Id="176" Count="0" />
      <LineId Id="149" Count="0" />
      <LineId Id="189" Count="0" />
      <LineId Id="184" Count="0" />
      <LineId Id="183" Count="0" />
      <LineId Id="185" Count="0" />
      <LineId Id="9" Count="0" />
    </LineIds>
    <LineIds Name="FB_CartCtrlMotionCar.forceDataAnalysis">
      <LineId Id="87" Count="14" />
      <LineId Id="26" Count="0" />
    </LineIds>
    <LineIds Name="FB_CartCtrlMotionCar.init">
      <LineId Id="79" Count="0" />
      <LineId Id="78" Count="0" />
      <LineId Id="11" Count="0" />
      <LineId Id="25" Count="0" />
      <LineId Id="24" Count="0" />
      <LineId Id="26" Count="0" />
      <LineId Id="28" Count="0" />
      <LineId Id="27" Count="0" />
      <LineId Id="29" Count="0" />
      <LineId Id="35" Count="0" />
      <LineId Id="33" Count="0" />
      <LineId Id="192" Count="3" />
      <LineId Id="197" Count="0" />
      <LineId Id="201" Count="2" />
      <LineId Id="196" Count="0" />
      <LineId Id="23" Count="0" />
      <LineId Id="21" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>